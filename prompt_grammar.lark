start : (extra_networks_prompts | top_prompts)*

?extra_networks_prompts : extra_networks
                        | extra_networks_prompts (extra_networks)+               -> combination
                        | extra_networks_prompts ("," [extra_networks_prompts]) -> multiple
extra_networks          : "<" extra_networks_name extra_networks_args ">"
extra_networks_name     : EXTRA_NETWORKS_TEXT
extra_networks_args     : (":" EXTRA_NETWORKS_TEXT)+

?top_prompts : top_prompt
             | top_prompts (top_prompt)+       -> combination
             | top_prompts ("," [top_prompts]) -> multiple
?top_prompt  : emphasized
             | scheduled
             | alternate
             | plain ":" plain         -> rating_prompt
             | plain
             | whitespace

?prompts : prompt
         | prompts (prompt)+       -> combination
         | prompts ("," [prompts]) -> multiple
?prompt  : emphasized
         | scheduled
         | alternate
         | plain
         | whitespace

alternate  : "[" prompts ("|" prompts)+ "]"
scheduled  : "[" prompts ":" prompts ":" number "]" -> scheduled_full
           | "[" prompts ":" ":" number "]"         -> scheduled_from
           | "[" prompts ":" number "]"             -> scheduled_to
           | "[" ":" prompts ":" number "]"         -> scheduled_none_to
emphasized : "(" prompts ":" number ")"             -> emphasized_weighted
           | "(" prompts ")"                        -> emphasized_positive
           | "[" prompts "]"                        -> emphasized_negative
plain      : PLAIN_TEXT
number     : NUMBER
whitespace : WHITESPACE

FLOAT  : INT | DECIMAL
NUMBER : ["+"|"-"] (FLOAT | INT)

EXTRA_NETWORKS_TEXT : /[^:>]+/
PLAIN_TEXT          : /(\\.|[^\[\]\(\)<>:|,\s])+/
WHITESPACE          : /\s+/

%import common.INT
%import common.DECIMAL
%import common.WS

%ignore WS